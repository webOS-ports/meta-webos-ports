BBPATH ?= ""
# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"
# We have recipe directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb ${LAYERDIR}/recipes-*/*/*.bbappend"

# Exclude unused recipies from meta-security, use only smack
BBMASK:append = " meta-security\/(?!.*\/smack\/).*"
# Disable the check added in: https://git.yoctoproject.org/cgit/cgit.cgi/meta-security/commit/?id=c1235f6affb5c38e64b3a04533b8388969b194b2
SKIP_META_SECURITY_SANITY_CHECK = "1"

BBFILE_COLLECTIONS += "meta-luneos"
BBFILE_PATTERN_meta-luneos := "^${LAYERDIR}/"
BBFILE_PRIORITY_meta-luneos = "20"

LAYERSERIES_COMPAT_meta-luneos = "scarthgap"

LAYERDEPENDS_meta-luneos = "\
  meta-luneui \
  qt6-layer \
  networking-layer \
  multimedia-layer \
  android-layer \
  clang-layer \
  security \
"

# Remove openembedded-layer perl-layer networking-layer meta-python
# dependencies from meta-security layer
# OVERRIDE for LAYERDEPENDS_security set in the meta-security layer
LAYERDEPENDS_security = "core"

addpylib ${LAYERDIR}/lib webos

SIGGEN_EXCLUDERECIPES_ABISAFE += " \
  luna-surfacemanager-conf \
  ofono-conf \
  rng-tools \
"

SIGGEN_EXCLUDE_SAFE_RECIPE_DEPS += " \
  webos-initscripts->systemd-machine-units \
  configd->configd-data \
  umediaserver->umediaserver-configs \
"

LICENSE_PATH += "${LAYERDIR}/licenses"

# Needs to use inherit_defer after:
# https://git.openembedded.org/openembedded-core/diff/meta/classes-recipe/packagegroup.bbclass?id=451363438d38bd4552d5bcec4a92332f5819a5d4
# or else nativesdk is inherritted after deferred allarch from packagegroup causing:
# ERROR: /OE/build/luneos-scarthgap/meta-qt6/recipes-qt/packagegroups/nativesdk-packagegroup-qt6-toolchain-host.bb: QA Issue: nativesdk-packagegroup-qt6-toolchain-host: native/nativesdk class is not inherited last, this can result in unexpected behaviour. Classes inherited after native/nativesdk: allarch.bbclass [native-last]
# ERROR: /OE/build/luneos-scarthgap/meta-qt6/recipes-qt/packagegroups/nativesdk-packagegroup-qt6-toolchain-host.bb: Fatal QA errors were found, failing task.
BBMASK += "meta-qt6/recipes-qt/packagegroups/nativesdk-packagegroup-qt6-toolchain-host.bb"
BBMASK += "meta-qt6/recipes-qt/packagegroups/nativesdk-packagegroup-qt6-toolchain-host-commercial.bb"

DESCRIPTION = "LuneOS QML components"
LICENSE = "LGPL-2.1"
LIC_FILES_CHKSUM = "file://COPYING;md5=d32239bcb673463ab874e80d47fae504"

PV = "0.4.2+git${SRCPV}"
SRCREV = "f4de327db84c5b38346110baf9a3a53905907be9"

DEPENDS = "qtbase qtdeclarative qtlocation qtquickcontrols qtquickcontrols2 luna-service2 libwebos-application kf5bluezqt-mer qtdeclarative-native qtwebengine"

RRECOMMENDS:${PN} += "qtlocation"

SRC_URI = "${WEBOS_PORTS_GIT_REPO_COMPLETE}"
S = "${WORKDIR}/git"

inherit qmake5
inherit webos_ports_repo
inherit webos_filesystem_paths
inherit pkgconfig

PACKAGES += "${PN}-examples"
FILES:${PN} += " \
    ${OE_QMAKE_PATH_QML}/LuneOS/ \
    ${OE_QMAKE_PATH_QML}/QtQuick/Controls.2/LuneOS/ \
    ${OE_QMAKE_PATH_QML}/QtQuick/Controls/Styles/LuneOS/ \
    ${OE_QMAKE_PATH_QML}/LunaWebEngineViewStyle/ \
"
FILES:${PN}-examples += " \
    ${webos_applicationsdir}/org.luneos.components.gallery \
"

do_configure:prepend() {
    # Dynamically set the Qt and Chromium versions based on exported values in below files generated by QtWebEngine's bbappend
    QT_VERSION=`cat ${STAGING_DATADIR}/qtwebengine/qt-version.txt`
    CHROMIUM_VERSION=`cat ${STAGING_DATADIR}/qtwebengine/chromium-version.txt`

    sed -i "s|@QT_VERSION@|${QT_VERSION}|" ${S}/modules/LuneOS/Components/UserAgent.qml
    sed -i "s|@CHROMIUM_VERSION@|${CHROMIUM_VERSION}|" ${S}/modules/LuneOS/Components/UserAgent.qml
}
